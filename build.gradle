plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.minecrell.licenser' version '0.3'
}

archivesBaseName = name.toLowerCase()

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-core:2.8.1'
    compile 'org.jline:jline-reader:3.5.1'
    runtime 'org.jline:jline-terminal-jna:3.5.1'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    required { !version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(tasks.uploadArchives) }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    url = project.url

                    scm {
                        url = project.url
                        connection = "scm:git:${project.url}.git"
                        developerConnection = "scm:git:${project.url}.git"
                    }

                    issueManagement {
                        system = 'GitHub Issues'
                        url = "${project.url}/issues"
                    }

                    developers {
                        developer {
                            id = 'minecrell'
                            name = 'Minecrell'
                            email = 'dev@minecrell.net'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}
